compilation_unit  = 
 [ package_statement ] 
 < import_statement > 
 < type_declaration >  . 

package_statement  = 
 "package" package_name  ";"  . 

import_statement  = 
 "import"  (  ( package_name  "."  "*"  ";"  ) 
 /  ( class_name )  )  ";"  . 

type_declaration  = 
 class_declaration  / interface_declaration . 

class_declaration  = 
 < modifier >  "class" IDENTIFIER 
 [  "extends" class_name ] 
 [  "implements" interface_name  <  "," interface_name >  ] 
 "{"  < field_declaration >  "}"  . 

interface_declaration  = 
 < modifier >  "interface" IDENTIFIER 
 [  "extends" interface_name  <  "," interface_name >  ] 
 "{"  < field_declaration >  "}"  . 

field_declaration  = 
 (  method_declaration 
 / constructor_declaration 
 / variable_declaration ) 
 / static_initializer 
 /  ";"  . 

method_declaration  = 
 < modifier > type IDENTIFIER 
 "("  [ parameter_list ]  ")"  <  "["  "]"  > 
 ( statement_block  /  ";"  )  . 

constructor_declaration  = 
 < modifier > IDENTIFIER  "("  [ parameter_list ]  ")" 
statement_block  . 

statement_block  =  "{"  < statement >  "}"  . 

variable_declaration  = 
 < modifier > type variable_declarator 
 <  "," variable_declarator  >  ";"  . 

variable_declarator  = 
 IDENTIFIER  <  "["  "]"  >  [  "=" variable_initializer ]  . 

variable_initializer  = 
expression 
 /  (  "{"  [ variable_initializer 
 <  "," variable_initializer  >  [  ","  ]  ]  "}"  )  . 

static_initializer  = 
 "static" statement_block  . 

parameter_list  = 
parameter  <  "," parameter >  . 

parameter  = 
type IDENTIFIER  <  "["  "]"  >  . 

statement  = 
variable_declaration 
 /  ( expression  ";"  ) 
 /  ( statement_block ) 
 /  ( if_statement ) 
 /  ( do_statement ) 
 /  ( while_statement ) 
 /  ( for_statement ) 
 /  ( try_statement ) 
 /  ( switch_statement ) 
 /  (  "synchronized"  "(" expression  ")" statement  ) 
 /  (  "return"  [ expression ]  ";"  ) 
 /  (  "throw" expression ";"  ) 
 /  (  "break"  [ IDENTIFIER ]  ";"  ) 
 /  (  "continue"  [ IDENTIFIER ]  ";"  ) 
 /  (  ";"  )  . 

if_statement = 
 "if"  "(" expression  ")" statement_block 
 [  "else" statement ]  . 

do_statement = 
 "do" statement_block  "while"  "(" expression  ")"  ";"  . 

while_statement = 
 "while"  "(" expression  ")" statement  . 

for_statement = 
 "for"  "("  ( variable_declaration  /  ( expression  ";"  )  /  ";"  ) 
 [ expression ]  ";" 
 [ expression ] 
 ")" statement  . 

try_statement = 
 "try" statement_block 
 <  "catch"  "(" parameter  ")" statement_block > 
 [  "finally" statement_block ]  . 

switch_statement = 
 "switch"  "(" expression  ")"  "{" 
 <  (  "case" expression  ":"  ) 
 /  (  "default"  ":"  ) 
 / statement > 
 "}"  . 

expression  = 
	[expressionPrefix] 
		expressionMain
		< "." expressionMain >
	[expressionPost].

expressionMain =
	  "null"
	/ "this"
	/ ( "super" [ "(" [ arglist ] ")" ] )
	/ ( IDENTIFIER [ "(" [ arglist ] ")" ] < "[" expression "]" > )
	/ ( "(" expression ")" ) 
	/ "true"
	/ "false"
	/ integer_literal 
	/ FLOAT 
	/ STRING 
	/ (
		"new" (
			  ( class_name "(" [arglist] ")" )
			/ ( type_specifier "[" [ expression ] "]" < "[" "]" > )
			/ ( "(" expression ")" )
		)
	  ).

expressionPrefix =
   "-" 
 / "++" 
 / "--"
 / "!"
 / "~".

expressionPost =
   "++"
 / "--"
 / ( (
	  "="
	/ "+" 
    / "+=" 
    / "-" 
    / "-=" 
    / "*" 
    / "*=" 
    / "/" 
    / "/=" 
    / "%" 
    / "%="
	/  ">" 
    / "<" 
    / ">=" 
    / "<=" 
    / "==" 
    / "!="
	/ "&" 
    / "&=" 
    / "|" 
    / "|=" 
    / "^" 
    / "^=" 
    / "&&" 
    / "||=" 
	/ ("?" expression ":")
	/ ">>="
	/ "<<"
	/ ">>"
	/ ">>>"
   ) expression )
   .

arglist  = 
expression  <  "," expression  >  . 

type  = 
type_specifier  <  "["  "]"  >  . 

type_specifier = 
 "boolean" 
 /  "byte" 
 /  "char" 
 /  "short" 
 /  "int" 
 /  "float" 
 /  "long" 
 /  "double" 
 / classOrInterface_name . 

modifier = 
 "public" 
 /  "private" 
 /  "protected" 
 /  "static" 
 /  "final" 
 /  "native" 
 /  "synchronized" 
 /  "abstract" 
 /  "threadsafe" 
 /  "transient"  . 

package_name = <IDENTIFIER "." > IDENTIFIER  . 

class_name  = <IDENTIFIER "." > IDENTIFIER  . 

interface_name = <IDENTIFIER "." > IDENTIFIER  .

classOrInterface_name = <IDENTIFIER "." > IDENTIFIER  .

integer_literal = 
 ( INT [ "l" ] )
 / ( "0x" HEX <HEX> ).

